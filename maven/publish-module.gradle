apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId "eu.kevin.android"
                artifactId PUBLISH_ARTIFACT_ID
                version sdkVersion
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "Official Kevin. Android SDK"
                    url = "https://github.com/getkevin/kevin-android"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "http://www.opensource.org/licenses/mit-license.php"
                        }
                    }
                    developers {
                        developer {
                            id = "mobile-kevin-dev"
                            name = "Kevin mobile developers"
                            email = "mobile@kevin.eu"
                        }
                    }
                    scm {
                        connection = "scm:git:github.com/getkevin/kevin-android.git"
                        developerConnection = "scm:git:ssh://github.com/getkevin/kevin-android.git"
                        url = "https://github.com/getkevin/kevin-android/tree/master"
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["mavenSigningKeyId"],
            rootProject.ext["mavenSigningKey"],
            rootProject.ext["mavenSigningKeyPassword"],
    )
    sign publishing.publications
}